# ############################################################################
# (c) Chancellery of the Prime Minister 2012-2015                            #
#                                                                            #
# Authors: Marta Retkiewicz                                                  #
# ############################################################################
# Simple CGE model Ballard Fullerton
# ############################################################################


options
{
    output logfile = TRUE;
    output LaTeX = TRUE;
    output LaTeX landscape = TRUE;
};

indexsets
{
    SEC = { 'M', 'N'};
    HH = { 'R', 'P' };
};

block <h::HH> HOUSEHOLD
{
    definitions
    {
        u<h>[] = (SUM<s::SEC>(alpha<h, s>^(1/sigma<h>) * D<h, s>[]^((sigma<h> - 1)/sigma<h>)))^(sigma<h>/(sigma<h> - 1));
    };
    controls
    {
        <s::SEC>D<h, s>[]; # Household h demand for good s
    };
    objective
    {
        U<h>[] = u<h>[];
    };
    constraints
    {
        SUM<s::SEC>(D<h, s>[] * p<s>[]) = K<h>[] * p_k[] + L<h>[]
            + SUM<s::SEC>(pi<s>[]) * phi<h>;
              # phi - household h share in firms' profits
    };
    identities
    {
        <hi::HH>K<hi>[] = par_k<hi>;
        <hi::HH>L<hi>[] = par_l<hi>;
    };
};

block <s::SEC> FIRM
{
    controls
    {
        Y<s>[], K<s>[], L<s>[];
    };
    objective
    {
        pi<s>[] = Y<s>[] * p<s>[] - (K<s>[] * p_k[] + L<s>[] );
    };
    constraints
    {
        Y<s>[] = phi<s> * (delta<s>^(1/sigma<s>)*L<s>[]^((sigma<s>-1)/sigma<s>)+ (1-delta<s>)^(1/sigma<s>)*K<s>[]^((sigma<s>-1)/sigma<s>) )^(sigma<s>/(sigma<s>-1)) ;
    };
};

block EQUILIBRIUM
{
    identities
    {
        <s::SEC>Y<s>[] = SUM<h::HH>D<h, s>[] ;
        SUM<h::HH>L<h>[] = SUM<s::SEC>(L<s>[]);
        
    };
};
