# ############################################################################
# This file is a part of gEcon.iosam.                                        #
#                                                                            #
# (c) Chancellery of the Prime Minister of the Republic of Poland 2014-2015  #
# (c) Marta Retkiewicz 2015-2016                                             #
# Licence terms can be found in the file:                                    #
# http://gecon.r-forge.r-project.org/files/gEcon.iosam_licence.txt           #
#                                                                            #
# Author: Marta Retkiewicz                                                   #
# ############################################################################
# Example for gEcon.iosam package: calibration of a CGE model para SAM Chile 2008
# ############################################################################


options
{
    output LaTeX = TRUE;
    output LaTeX landscape = TRUE;
    output logfile = TRUE;
};


indexsets
{
    SEC = { 'Actprim', 'Actmanu', 'Actserv' };
	PRODS={'Prodprim', 'Prodmanu', 'Prodserv'};
};


tryreduce
{
    <s::SEC>K<s>[], <s::SEC>L<s>[];
};

block CONSUMER
{
  ### VARIABLES

    # k_total_data				Capital total
	# par_k_h					% particip hogar en capital
	# l_total_data				Trabajo total
	#L_h[]						Trabajo de los hogares
	#por_l_h					% del trabajo de los hogares
	#K_h[]						Capital perteneciente a los hogares
	#D<p>[]						Demanda de los hogares por el producto p
	#SAV[]						Ahorro de los hogares
	#CONT_SOC[]					Contribuciones sociales de los hogares (dadas por los hogares a las empresas y al gobierno)
	#TR_EMP[]					Transferencias desde las empresas
	#TR_GOV[]					Transferencias desde el gobierno
	#PREST_SOC[]				Prestaciones sociales (entregadas por el gobierno y las empresas)
	#pro_prest
	
	
	
    definitions
    {
        u[] = (SUM<p::PRODS>(alpha<p> * D<p>[]^((omega - 1)/omega)))^(omega/(omega - 1));
    };

    controls
    {
        <p::PRODS>D<p>[];
    };

    objective
    {
        U[] = u[];
    };
    constraints
    {
	
        INC[] = SUM<p::PRODS>((D<p>[] * p<p>[]*(1+vat<p>)))+SAV[]+CONT_SOC[];
		INC[] = BTINC[]+SUM<s::SEC>(pi<s>[]) - pit * PIT_base[];
		BTINC[] = K_h[] * p_k[] + L_h[] + TR_EMP[]+TR_GOV[]+PREST_SOC[];
		#PIT_base[] = BTINC[];
		PIT_base[] = K_h[] * p_k[] + L_h[];
		
    };
    identities
    {
        
        K_h[] = k_total_data*par_k_h;
        L_h[] = l_total_data;
		SAV[]=por_sav*INC[];
		CONT_SOC[]=por_cont*INC[];

		# Se va a hacer depender las transferencias de los ingresos del gobierno y las empresas
		TR_EMP[]=por_tremp*ING_F[]; 
		TR_GOV[]=por_trgov*ING_GOB[];
		
		PREST_SOC[]=PREST_SOC_F[]+PREST_SOC_G[];
		
		
    };
	calibration
    {
    
        <p::PRODS\'Prodprim'>D<p>[] = d_data<p>/(1+vat<p>) -> alpha<p>; #OJO con el rol del  IVA
		SUM<p::PRODS>(alpha<p>^omega) = 1 -> alpha<'Prodprim'>;

    };
};


block <s::SEC>SECTOR
{
  ### VARIABLES

    # por_ps[p,s]				% de la produccion del producto p en el sector s

	#data_prod_s<s,p>			la produccion de p en s
	
	
	
    controls
    {
		<p::PRODS>Y<p,s>[], <p::PRODS>K<p,s>[], <p::PRODS>L<p,s>[];
       
    };

    objective
    {
         #pi<s>[] =  (SUM<p::PRODS>(Y<p,s>[] * p<p>[]) - (K<s>[] * p_k[] + L<s>[]) );
         pi<s>[] =  SUM<p::PRODS>(Y<p,s>[] * p<p>[] -K<p,s>[] * p_k[] - L<p,s>[]) ;
         
    };
    constraints
    {
        <p::PRODS>Y<p,s>[] = gamma<p,s> * (K<p,s>[]^beta_k<p,s>) * (L<p,s>[]^beta_l<p,s>);

    };
    
	identities
	{
		K<s>[]=SUM<p::PRODS>(K<p,s>[]);
		L<s>[]=SUM<p::PRODS>(L<p,s>[]);
		
		
	};
	
	calibration
    {
		
		<p::PRODS>beta_k<p,s> + beta_l<p,s> = 1 -> beta_k<p,s>;
		<p::PRODS>L<p,s>[] = l_data<p,s> -> beta_l<p,s>;
		<p::PRODS>Y<p,s>[] = data_prod_s<s,p> -> gamma<p,s>;

    };
};


block <p::PRODS>PRODUCTS
{
  ### VARIABLES


	#G<p>[]					Gasto del gobierno en el bien p
	#I<p>[]					Gasto en inversi√≥n en el bien p
	# DA<p>[] 				demanda agregada del bien. Esta en pesos despues de iva
	#OA<p>[]				oferta agregada del bien. Esta en pesos antes de iva

 
	    identities
    {
		OA<p>[]*(1+vat<p>)=DA<p>[];
		OA<p>[]=SUM<s::SEC>(Y<p,s>[]*p<p>[]);
		
		DA<p>[]=p<p>[]*(1+vat<p>)*(D<p>[]  +GG<p>[] +I<p>[] );


		#GG<p> lo voy a definir en el bloque del gobierno
		#I<p> lo voy a definir en el bloque ahorro inversion
		
	};

};


block FIRM
{

  ### VARIABLES

    # K_f[]				capital de las empresas
	#par_k_f			% participacion de las empresas en el capital
	#fit                impuesto sobre los ingresos del capital de las empresas
	#ING_F[]			ingresos totales de las empresas
	#por_sav_f			% del ahorro de las empresas (como % de sus ingresos)
	#SAV_F[]			Ahorro de las empresas
	#CONT_SOC_F[]		Contribuciones sociales pagadas a las empresas
	#por_cont_soc_f		% de las contribuciones sociales pagadas a las empresas
	#PREST_SOC_F[]		Prestaciones sociales de las empresas
	


 identities
    {
	
	ING_F[]=TR_EMP[]+ PREST_SOC_F[]+fit*(K_f[]*p_k[])+SAV_F[];
	ING_F[]=K_f[]*p_k[]+CONT_SOC_F[];
	SAV_F[]=por_sav_f*ING_F[];
	K_f[] = k_total_data*par_k_f;
	CONT_SOC_F[]=CONT_SOC[]*por_cont_soc_f;

	PREST_SOC_F[]=ING_F[]*por_pres_soc_f;
	
	};

};


block SI
{
  ### VARIABLES

	#SAV_TOT[]		Ahorros totales
	#por_inv_p<p>		porcentaje de la demanda agregada del bien p en ahorro 
	
	
	
identities
	{

	SAV_TOT[]=SUM<p::PRODS>(I<p>[] * p<p>[]*(1+vat<p>));
	SAV_TOT[]=SAV[]+SAV_F[]+SAV_G[];


	};

};



block GOB
{
  ### VARIABLES
  
	#ING_GOB[]			Ingresos Totales del gobierno
	#par_k_g			% participacion del gobierno en el capital
	#K_g[]				Capital del gobierno
	#por_cont_soc_g		% de las contribuciones sociales pagasdas al gobierno
	#CONT_SOC_G[]		Contrib soc recibidas por el gobierno
	#TOTAl_tax[]		Impuestos totales
	#por_gg_p<p>		% de la demanda en gasto de gobierno
	#data_gg_p<p>		Gasto de gobierno en  el producto p
	#PREST_SOC_G[]
	#por_pres_soc_g		% de las prestaciones sociales pagadas por el gobierno
	#VAT[]				IVA
	#DIR_T[]			Impuesto a la renta

	#por_sav_g			% de ahorro del gobierno
  
identities
	{
	ING_GOB[]=GTO_G[];
	ING_GOB[]=K_g[]*p_k[]+CONT_SOC_G[]+TOTAL_TAX[];
	GTO_G[]=SUM<p::PRODS>(GG<p>[] * p<p>[]*(1+vat<p>))+TR_GOV[]+PREST_SOC_G[]+SAV_G[];
	PREST_SOC_G[]=ING_GOB[]*por_pres_soc_g;
	
	K_g[] = k_total_data*par_k_g;
	CONT_SOC_G[]=CONT_SOC[]*por_cont_soc_g;
	VAT[]=SUM<p::PRODS>((vat<p>* p<p>[])*(D<p>[] +GG<p>[] +I<p>[] ));
	DIR_T[]=pit * PIT_base[]+fit*(K_f[]*p_k[]);
	
	TOTAL_TAX[]=VAT[]+DIR_T[];
	SAV_G[]=por_sav_g*ING_GOB[];
	
		<p::PRODS>GG<p>[]* p<p>[]*(1+vat<p>)=data_gg_p<p>;	
	
	
	
	
	# Se fija la demanda de bienes del gobierno

	};
	

};


block EQUILIBRIUM
{
identities 
	{
		#SUM<s::SEC>L<s>[]=L_h[];
		#(SUM<s::SEC>K<s>[])=(K_h[]+K_f[]+K_g[]);
		(SUM<p::PRODS>(SUM<s::SEC>K<p,s>[]))=(K_h[]+K_f[]+K_g[]);

		
		
	};

};





